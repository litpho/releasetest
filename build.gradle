buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.4.0'
    }
}

apply plugin: 'net.researchgate.release'

release {
    buildTasks = ['build']
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.21'
    }

    rootProject.tasks.runBuildTasks.dependsOn build
    rootProject.tasks.afterReleaseBuild.dependsOn publish

    publishing {
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url "${nexusSnapshotRepository}"
                } else {
                    url "${nexusReleaseRepository}"
                }
                credentials {
                    username "${nexusUsername}"
                    password "${nexusPassword}"
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':b') {
    dependencies {
        compile project(':a')
    }
}
